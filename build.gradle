buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

group = "com.mixpanel.android"
version = getVersionName()

apply plugin: 'android-library'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 18
        testPackageName "com.mixpanel.android"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
}

/***
 * This will specify that we want to publish our aar file when we run gradle publish / gradle publishToMavenLocal
 */
android.libraryVariants
publishing {
    publications {
        maven(MavenPublication) {
            artifact "./build/libs/${project.name}-${version}.aar"
        }
    }
}
